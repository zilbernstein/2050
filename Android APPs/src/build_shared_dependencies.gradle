def dependencyGroup(Closure closure) {
    closure.delegate = dependencies
    return closure
}

ext {
    // Support library version
    supportLibraryVersion = '26.1.0'

    // Dagger
    depsDagger = dependencyGroup {
        implementation "com.google.dagger:dagger:2.13"
        kapt "com.google.dagger:dagger-compiler:2.13"
    }

    // Kotlin
    depsKotlin = dependencyGroup {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    }

    // AppCompat
    depsAppCompat = dependencyGroup {
        implementation 'androidx.appcompat:appcompat:1.1.0'
    }

    // RxKotlin
    depsRxKotlin = dependencyGroup {
        implementation 'io.reactivex.rxjava2:rxkotlin:2.1.0'
        implementation "io.reactivex.rxjava2:rxandroid:2.0.1"
    }

    // Arch
    depsArch = dependencyGroup {
        implementation "androidx.lifecycle:lifecycle-extensions:2.1.0"
        implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.1.0"
    }

    // Ktx
    depsKtx = dependencyGroup {
        implementation "androidx.core:core-ktx:1.1.0"
    }

    // MapStruct
    depsMapStruct = dependencyGroup {
        implementation 'org.glassfish:javax.annotation:3.1.1'
        implementation 'org.mapstruct:mapstruct:1.2.0.Final'
        kapt 'org.mapstruct:mapstruct-processor:1.2.0.Final'
    }

    // Logger
    depsLogger = dependencyGroup {
        implementation 'ru.digipeople.logger:logger-kotlin:1.0.0'
    }

    //Gson
    depsGson = dependencyGroup {
        api 'com.google.code.gson:gson:2.8.5'
    }

    //AppCenter
    depsAppCenter = dependencyGroup {
        implementation "com.microsoft.appcenter:appcenter-analytics:3.0.0"
        implementation "com.microsoft.appcenter:appcenter-crashes:3.0.0"
        implementation "com.microsoft.appcenter:appcenter-distribute:3.0.0"
    }

    // Coroutines
    depsCoroutines = dependencyGroup {
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.0.0'
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.0.0'
        implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-rx2:1.3.3'
    }

    //Firebase
    depsFirebase = dependencyGroup {
        implementation 'com.google.firebase:firebase-messaging:20.0.1'
        implementation 'com.google.firebase:firebase-core:17.2.1'
    }

    // Tests
    depsTests = dependencyGroup {
        // JUnit
        testImplementation "junit:junit:4.12"
        // Test runner
        androidTestImplementation "androidx.test:runner:1.0.1"
        // Espresso
        androidTestImplementation("androidx.test.espresso:espresso-core:3.1.0", {
            exclude module: 'support-annotations'
        })
    }
}

dependencies {
    //Kotlin
    depsKotlin()
    // Coroutines
    depsCoroutines()
    //Logger
    depsLogger()
}